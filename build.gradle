plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'io.zksync'
version System.getenv('VERSION')
sourceCompatibility = '1.8'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'zksync'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'ZkSync SDK'
                description = 'This repository provides a Java SDK for zkSync developers, which can be used either on PC or Android.'
                url = 'https://github.com/zksync-sdk/zksync-java'
                organization {
                    name = 'Matter Labs'
                    url = 'https://matter-labs.io'
                }
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://www.mit.edu/~amini/LICENSE.md'
                    }
                }
                developers {
                    developer {
                        id = 'mfischuk'
                        name = 'Maxim Fischuk'
                        email = 'mfischuk@vareger.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/zksync-sdk/zksync-java'
                    url = 'https://github.com/zksync-sdk/zksync-java'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username findProperty("sonatypeUsername")
                password findProperty("sonatypePassword")
            }
        }
    }
}


signing {
    def signingKeyId = findProperty("signingKeyId")
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
    sign publishing.publications.mavenJava
}

dependencies {

    // === Code Generation ===
    compileOnly 'org.projectlombok:lombok:1.18.16'
    annotationProcessor 'org.projectlombok:lombok:1.18.16'

    // === DTO ===
    implementation('org.modelmapper:modelmapper:1.1.0')

    // === Utils ===
    implementation('org.apache.commons:commons-lang3:3.7')
    implementation('commons-validator:commons-validator:1.6')
    implementation('org.jsoup:jsoup:1.11.3')

    // === Http Client ===
    implementation 'com.squareup.okhttp3:okhttp:4.8.0'

    // === Web3 client ===
    implementation 'org.web3j:core:4.8.7'

    // === Web3 abi ===
    implementation 'org.web3j:abi:4.8.7'

    // === For Eth Signing ===
    implementation 'org.web3j:crypto:4.8.7'

    // === Test dependencies ===
    testImplementation 'junit:junit:4.13.1'
    testImplementation "org.mockito:mockito-inline:+"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
}
